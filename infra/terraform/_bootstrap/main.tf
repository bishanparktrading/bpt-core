// State bucket. Versioning ON so a corrupted apply can be rolled back.
resource "aws_s3_bucket" "tfstate" {
  bucket = var.state_bucket_name

  // No force_destroy: protects against terraform destroy nuking history.
  // To delete the bucket: empty it manually first, then `terraform destroy`.
}

resource "aws_s3_bucket_versioning" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "tfstate" {
  bucket = aws_s3_bucket.tfstate.id
  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "tfstate" {
  bucket                  = aws_s3_bucket.tfstate.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

// DynamoDB lock table. Pay-per-request: state ops are infrequent enough
// that provisioned throughput would be wasted spend.
resource "aws_dynamodb_table" "tfstate_lock" {
  name         = var.state_lock_table_name
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }
}
